plugins {
	id 'java' // Needed for assemble and build
	id 'maven-publish' // Provides publishing and publishToMavenLocal
	id 'com.enonic.xp.app' version '3.4.0'
	id 'com.github.node-gradle.node' version '7.0.0'
}


repositories {
	mavenLocal()
	xp.enonicRepo('dev')
	mavenCentral()
	jcenter()
}


app {
	name = project.appName
	displayName = 'Page HTML5'
	vendorName = 'Enonic AS'
	vendorUrl = 'http://enonic.com'
	systemVersion = "${xpVersion}"
}


dependencies {
	implementation "com.enonic.xp:core-api:${xpVersion}"
	implementation "com.enonic.xp:portal-api:${xpVersion}"

	include "com.enonic.xp:lib-portal:${xpVersion}"

	include 'com.enonic.lib:lib-util:2.2.1'
}


node {
	// Whether to download and install a specific Node.js version or not
	// If false, it will use the globally installed Node.js
	// If true, it will download node using above parameters
	// Note that npm is bundled with Node.js
	download = true

	// Version of node to download and install (only used if download is true)
	// It will be unpacked in the workDir
	version = '18.18.0'
}


sourceSets {
	main {
		java {
			srcDir 'src/main/java'
		}
		resources { // Paths relative to src/main/resources
			exclude '**/*.es' // Avoid having your es files copied into ./build
		}
	}
}


wrapper {
	distributionUrl = "${gradleDistributionUrl}"
}


task webpack(type:NodeTask) {
	script = file('node_modules/webpack-cli/bin/cli.js')
	args = [
		'--color',
		'--config', 'webpack.config.babel.js'
	]
}
webpack.dependsOn yarn_install
webpack.mustRunAfter yarn_install
assemble.dependsOn webpack
assemble.mustRunAfter webpack


task watch(type:NodeTask) {
	script = file('node_modules/webpack-cli/bin/cli.js')
	args = [
		'--color',
		'--config', 'webpack.config.babel.js',
		'--watch'
	]
}
