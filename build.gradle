plugins {
    id 'java' // Needed for assemble and build
    id 'maven-publish' // Provides publishing and publishToMavenLocal
    id 'com.enonic.xp.app' version '2.0.0'
    id 'com.moowork.node' version '1.3.1'
}


repositories {
    mavenLocal()
    jcenter()
    xp.enonicRepo()
}


app {
    name = project.appName
    displayName = 'Page HTML5'
    vendorName = 'Enonic AS'
    vendorUrl = 'http://enonic.com'
    systemVersion = "${xpVersion}"
}


dependencies {
    compile "com.enonic.xp:core-api:${xpVersion}"
    compile "com.enonic.xp:portal-api:${xpVersion}"

    include "com.enonic.xp:lib-portal:${xpVersion}"

    include 'com.enonic.lib:lib-util:2.2.1'
}


node {
    version "${nodeVersion}"
    download true
}


sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources { // Paths relative to src/main/resources
            exclude '**/*.es' // Avoid having your es files copied into ./build
        }
    }
}


wrapper {
    distributionUrl = "${gradleDistributionUrl}"
}


task webpack(type:NodeTask) {
    script = file('node_modules/webpack-cli/bin/cli.js')
    args = [
        '--color',
        '--config', 'webpack.config.babel.js'
    ]
}
webpack.dependsOn yarn_install
webpack.mustRunAfter yarn_install
assemble.dependsOn webpack
assemble.mustRunAfter webpack


task watch(type:NodeTask) {
    script = file('node_modules/webpack-cli/bin/cli.js')
    args = [
        '--color',
        '--config', 'webpack.config.babel.js',
        '--watch'
    ]
}
